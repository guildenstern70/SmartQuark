/*
 * The SmartQuark Project
 * Copyright (c) Alessio Saltarin, 2021.
 * This software is licensed under MIT License
 * See LICENSE
 */

plugins {
    id 'java'
    id 'io.quarkus'
    id "org.jetbrains.kotlin.jvm" version "1.4.20"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.4.20"
    id "org.jetbrains.kotlin.plugin.noarg" version "1.4.20"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    implementation "io.quarkus:quarkus-agroal:${quarkusPlatformVersion}"
    implementation "io.quarkus:quarkus-qute:${quarkusPlatformVersion}"
    implementation "io.quarkus:quarkus-resteasy:${quarkusPlatformVersion}"
    implementation "io.quarkus:quarkus-resteasy-jackson:${quarkusPlatformVersion}"
    implementation "io.quarkus:quarkus-resteasy-qute:${quarkusPlatformVersion}"
    implementation "io.quarkus:quarkus-kotlin:${quarkusPlatformVersion}"
    implementation "io.quarkus:quarkus-smallrye-openapi:${quarkusPlatformVersion}"
    implementation "io.quarkus:quarkus-config-yaml:${quarkusPlatformVersion}"
    implementation "io.quarkus:quarkus-hibernate-orm-panache-kotlin:${quarkusPlatformVersion}"
    implementation "io.quarkus:quarkus-jdbc-h2:${quarkusPlatformVersion}"
    implementation "io.quarkus:quarkus-jdbc-postgresql:${quarkusPlatformVersion}"
    implementation "io.quarkus:quarkus-smallrye-health:${quarkusPlatformVersion}"
    testImplementation "io.quarkus:quarkus-junit5:${quarkusPlatformVersion}"
    testImplementation "org.assertj:assertj-core:3.6.1"
    testImplementation "io.rest-assured:kotlin-extensions"
}

group 'net.littlelite.smartquark'
version '0.1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
    kotlinOptions.javaParameters = true
}

compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

quarkus {
    setOutputDirectory("$projectDir/build/classes/kotlin/main")
}

quarkusDev {
    setSourceDir("$projectDir/src/main/kotlin")
}

allOpen {
    annotation("javax.ws.rs.Path")
    annotation("javax.enterprise.context.ApplicationScoped")
    annotation("io.quarkus.test.junit.QuarkusTest")
    annotation("javax.persistence.Entity")
}

noArg {
    annotation("javax.persistence.Entity")
}

test {
    useJUnitPlatform()
    exclude '**/Native*'
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
